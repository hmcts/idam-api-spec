import io.swagger.codegen.DefaultGenerator
import io.swagger.codegen.config.CodegenConfigurator

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'io.swagger:swagger-codegen:2.2.3'
        classpath 'io.spring.gradle:dependency-management-plugin:1.0.5.RELEASE'
    }
}

plugins {
    id 'java'
    id 'maven-publish'
    id 'io.spring.dependency-management' version '1.0.5.RELEASE'
    id 'com.jfrog.bintray' version '1.8.0'
}


version = '2.5.3'
group = 'uk.gov.hmcts.reform.idam'

description = """idam-api-spec"""


sourceCompatibility = 1.8
targetCompatibility = 1.8


tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    maven {
        url 'https://dl.bintray.com/hmcts/hmcts-maven'
    }
    jcenter()
}

configurations {
    generatedCompile
    compile.extendsFrom generatedCompile
}

dependencyManagement {
    imports {
        mavenBom 'uk.gov.hmcts.reform.idam:idam-bom:1.2.0'
    }
}

dependencies {
    generatedCompile group: 'org.springframework', name: 'spring-web'
    generatedCompile group: 'javax.validation', name: 'validation-api'
    generatedCompile group: 'io.springfox', name: 'springfox-swagger2'
}

def swaggerSourceFile = 'src/main/resources/api.yaml'
def swaggerTargetFolder = 'src/generated/java'

task generateApi {
    inputs.file("$projectDir/$swaggerSourceFile")
    outputs.dir("$buildDir")
    doLast{
        def config = new CodegenConfigurator()
        config.setInputSpec("file:///$projectDir/$swaggerSourceFile")
        config.setOutputDir("$buildDir")
        config.setLang('spring')
        config.setAdditionalProperties([
                'generateApis'                 : 'false',
                'interfaceOnly'                : 'true',
                'apiPackage'                   : 'uk.gov.hmcts.reform.idam.api',
                'modelPackage'                 : 'uk.gov.hmcts.reform.idam.api.model',
                'sourceFolder'                 : swaggerTargetFolder,
                'dateLibrary'                  : 'java8',
                'generateModelDocumentation'   : 'false'
        ])
        new DefaultGenerator().opts(config.toClientOptInput()).generate()
    }
}

sourceSets {
    generated {
        java {
            srcDirs "$buildDir/$swaggerTargetFolder"
        }
    }
}

jar {
    from sourceSets.generated.output
}

compileGeneratedJava.dependsOn generateApi
classes.dependsOn generatedClasses
compileJava.dependsOn compileGeneratedJava

publishing {
    publications {
        Main(MavenPublication) {
            from components.java
            groupId project.group
            artifactId rootProject.name
            version project.version
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['Main']
    publish = true
    pkg {
        repo = 'hmcts-maven'
        name = "${rootProject.name}"
        userOrg = 'hmcts'
        licenses = ['MIT']
        vcsUrl = "https://github.com/hmcts/${rootProject.name}"
        version {
            name = project.version
        }
    }
}