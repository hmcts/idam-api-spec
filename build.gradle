import io.swagger.codegen.DefaultGenerator
import io.swagger.codegen.config.CodegenConfigurator

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'io.swagger:swagger-codegen:2.4.2'
        classpath 'io.spring.gradle:dependency-management-plugin:1.0.7.RELEASE'
    }
}

plugins {
    id 'java'
    id 'maven-publish'
    id 'io.spring.dependency-management' version '1.0.7.RELEASE'
    id 'com.jfrog.bintray' version '1.8.4'
}

version = '2.16.0'
group = 'uk.gov.hmcts.reform.idam'

description = """idam-api-spec"""

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    maven {
        url 'https://dl.bintray.com/hmcts/hmcts-maven'
    }
    jcenter()
}

configurations {
    generatedCompile
    compile.extendsFrom generatedCompile
}

dependencyManagement {
    imports {
        mavenBom 'uk.gov.hmcts.reform.idam:idam-bom:1.5.0'
    }
}

dependencies {
    generatedCompile group: 'org.springframework.boot', name: 'spring-boot-starter-web'
    generatedCompile group: 'org.springframework.boot', name: 'spring-boot-starter-tomcat'
    generatedCompile group: 'io.springfox', name: 'springfox-swagger2'
    generatedCompile group: 'io.springfox', name: 'springfox-swagger-ui'
    generatedCompile group: 'javax.validation', name: 'validation-api', version: '2.0.1.Final'
    generatedCompile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310'
}

project.ext {
    swaggerTargetFolder = 'src/generated/java'
}

def generateCodeFromSwagger(swaggerSourceFile, packageName, importMappings) {
    def config = new CodegenConfigurator()
    config.setInputSpec("file:///$projectDir/$swaggerSourceFile")
    config.setOutputDir("$buildDir")
    config.setLang('spring')
    config.setAdditionalProperties([
            'generateApis'                 : 'false',
            'interfaceOnly'                : 'true',
            'apiPackage'                   : 'uk.gov.hmcts.reform.idam.api.' + packageName,
            'modelPackage'                 : 'uk.gov.hmcts.reform.idam.api.' + packageName +'.model',
            'sourceFolder'                 : project.swaggerTargetFolder,
            'dateLibrary'                  : 'java8',
            'useTags'                      : (packageName == 'external' ? 'true' : 'false'),
            'generateModelDocumentation'   : 'false'
    ])
    if (importMappings != null) {
        ((LinkedHashMap<String, String>) importMappings).each { k, v -> config.addImportMapping(k, v) }
    }
    new DefaultGenerator().opts(config.toClientOptInput()).generate()
}

task generateApi {
    outputs.dir("$buildDir")
    doLast{
        generateCodeFromSwagger('src/main/resources/openid-connect.yaml', 'external', null)

        generateCodeFromSwagger('src/main/resources/shared.yaml', 'shared', null)

        generateCodeFromSwagger('src/main/resources/internal-admin-api.yaml', 'internal', [
                'User': 'uk.gov.hmcts.reform.idam.api.shared.model.User',
                'ErrorMap' : 'uk.gov.hmcts.reform.idam.api.shared.model.ErrorMap',
                'ArrayOfRoles' : 'uk.gov.hmcts.reform.idam.api.shared.model.ArrayOfRoles',
                'Role' : 'uk.gov.hmcts.reform.idam.api.shared.model.Role',
                'ArrayOfStrings' : 'uk.gov.hmcts.reform.idam.api.shared.model.ArrayOfStrings',
                'RoleDetail' : 'uk.gov.hmcts.reform.idam.api.shared.model.RoleDetail',
                'UpdateRole' : 'uk.gov.hmcts.reform.idam.api.shared.model.UpdateRole',
                'PatchRequest' : 'uk.gov.hmcts.reform.idam.api.shared.model.PatchRequest',
                'PatchOperation' : 'uk.gov.hmcts.reform.idam.api.shared.model.PatchOperation',
                'RegisterRequest' : 'uk.gov.hmcts.reform.idam.api.shared.model.RegisterRequest',
                'SelfRegisterRequest' : 'uk.gov.hmcts.reform.idam.api.shared.model.SelfRegisterRequest'
        ])

        generateCodeFromSwagger('src/main/resources/external-user-mgmt-api.yaml', 'external', [
                'RoleDefinition': 'uk.gov.hmcts.reform.idam.api.shared.model.RoleDefinition',
                'User': 'uk.gov.hmcts.reform.idam.api.shared.model.User',
                'ErrorMap' : 'uk.gov.hmcts.reform.idam.api.shared.model.ErrorMap'
        ])

        generateCodeFromSwagger('src/main/resources/external-dynamic-role-assign-api.yaml', 'external', [
                'RoleDefinition': 'uk.gov.hmcts.reform.idam.api.shared.model.RoleDefinition'
        ])

        generateCodeFromSwagger('src/main/resources/external-policies-api.yaml', 'external', null)
    }
}

sourceSets {
    generated {
        java {
            srcDirs "$buildDir/$swaggerTargetFolder"
        }
    }
}

jar {
    from sourceSets.generated.output
}

compileGeneratedJava.dependsOn generateApi
classes.dependsOn generatedClasses
compileJava.dependsOn compileGeneratedJava

publishing {
    publications {
        Main(MavenPublication) {
            from components.java
            groupId project.group
            artifactId rootProject.name
            version project.version
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['Main']
    publish = true
    pkg {
        repo = 'hmcts-maven'
        name = "${rootProject.name}"
        userOrg = 'hmcts'
        licenses = ['MIT']
        vcsUrl = "https://github.com/hmcts/${rootProject.name}"
        version {
            name = project.version
        }
    }
}
